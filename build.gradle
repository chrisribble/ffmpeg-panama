plugins {
	id 'com.github.ben-manes.versions' version '0.48.0'
	id 'com.google.osdetector' version '1.7.3'
	id 'se.solrike.sonarlint' version '1.0.0-beta.15' apply false
	id 'com.github.spotbugs' version '5.1.3' apply false
}


group = 'com.chrisribble.ffmpeg'
version = '0.1-SNAPSHOT'

wrapper {
	gradleVersion = '8.6'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'jacoco'
	apply plugin: 'maven-publish'
	apply plugin: "se.solrike.sonarlint"
	//apply plugin: 'com.github.spotbugs'

	java {
		sourceCompatibility = 21
		targetCompatibility = 21

		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}

	repositories {
		 mavenCentral()
	}

	dependencies {
		sonarlintPlugins libs.sonar.java.plugin
	}

	gradle.projectsEvaluated {
		tasks.withType(JavaCompile) { task ->
			options.compilerArgs.add('-Werror')
			options.compilerArgs.add('-Xlint:all')
			options.compilerArgs.add('-Xlint:-processing')
			options.compilerArgs.add('-Xlint:-preview')
			options.compilerArgs.add('--enable-preview')
		}
		tasks.withType(VerificationTask) { task ->
			if (task.name.startsWith("spotbugs")) {
				task.reports {
					html.required = true
				}
			}
		}
	}
}

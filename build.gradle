plugins {
	id 'com.github.ben-manes.versions' version '0.52.0'
	id 'se.solrike.sonarlint' version '2.2.0' apply false
	id 'com.github.spotbugs' version '6.1.11' apply false
}


group = 'io.github.chrisribble.ffmpeg'
version = '0.1-SNAPSHOT'

wrapper {
	gradleVersion = '9.0'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'jacoco'
	apply plugin: 'maven-publish'
	apply plugin: 'se.solrike.sonarlint'

	java {
		sourceCompatibility = 25
		targetCompatibility = 25

		toolchain {
			languageVersion = JavaLanguageVersion.of(25)
		}
	}

	repositories {
		 mavenCentral()
	}

	dependencies {
		sonarlintPlugins libs.sonar.java.plugin
	}

	gradle.projectsEvaluated {
		tasks.withType(JavaCompile) { task ->
			options.compilerArgs.add('-Werror')
			options.compilerArgs.add('-Xlint:all')
			options.compilerArgs.add('-Xlint:-processing')
			options.compilerArgs.add('-Xlint:-restricted')
		}
		tasks.withType(VerificationTask) { task ->
			if (task.name.startsWith("spotbugs")) {
				task.reports {
					html.required = true
				}
			}
		}
	}

	sonarlint {
		excludeRules = [
			'java:S100',   // Method name pattern for C type congruence
			'java:S125',   // Commented-out code false positives for C type info
			'java:S1135',  // TODO
			'java:S3864',  // peek in Stream delegate
			'java:S6548',	// Singleton
		]
	}
}

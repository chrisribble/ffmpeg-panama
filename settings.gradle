pluginManagement {
	repositories {
		// Prefer mavenCentral over gradlePluginPortal, which redirects to jcenter
		mavenCentral()
		gradlePluginPortal()
	}
}

plugins {
    // Apply the foojay-resolver plugin to allow automatic download of JDKs
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.8.0'
}

dependencyResolutionManagement {
	versionCatalogs {
		libs {
			def slf4j = version('slf4j', '2.0.11')

			library('slf4j-api', 'org.slf4j', 'slf4j-api').versionRef(slf4j)
			library('slf4j-simple', 'org.slf4j', 'slf4j-simple').versionRef(slf4j)
			library('testng', 'org.testng:testng:7.8.0')
			library('sonar-java-plugin', 'org.sonarsource.java:sonar-java-plugin:7.31.0.34839')
		}
	}
}


Closure discoverSubprojects = {
def list = []
	rootDir.eachDir(){ dir ->
		dir.eachFileMatch({it == 'build.gradle'}, { list += [dir.name] })
	}
	list.toArray(new java.lang.String[0])
}

rootProject.name = 'ffmpeg-panama'
include discoverSubprojects()

